@AbapCatalog.sqlViewName: 'ZWAGRPBC'
@AbapCatalog.compiler.compareFilter: true
@AbapCatalog.preserveKey: true
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'Relat√≥rio Bse Conhecimento'

@ObjectModel.modelCategory: #BUSINESS_OBJECT
@ObjectModel.compositionRoot: true
@ObjectModel.transactionalProcessingEnabled: true
@ObjectModel.createEnabled: true
@ObjectModel.updateEnabled: true
@ObjectModel.deleteEnabled: true
@ObjectModel.writeActivePersistence: 'zwagt_user'

@OData.publish: true
define view ZWAGCDS_RPRT_BASE_KNOW
  as select from zwagt_user as _user
  association [0..*] to zwagt_user_tools as _user_tools on $projection.Userid = _user_tools.userid
{
      @UI: { lineItem: [{ position: 10 }],
             selectionField: [{ position: 10 } ]}
      @Consumption.valueHelpDefinition: [{ entity:{name: 'ZWAGCDS_USERS_VH', element: 'Userid' } }]
  key _user.userid          as Userid,
      @UI: { lineItem: [{ position: 20 }],
             selectionField: [{ position: 20 } ]}
      @Consumption.valueHelpDefinition: [{ entity:{name: 'ZWAGCDS_USERS_VH', element: 'Email' } }]
      _user.email           as Email,
      @UI.lineItem: [{ position: 30 }]
      _user.fullname        as Fullname,
      @UI: { lineItem: [{ position: 40, label: 'Ferramenta' }],
             selectionField: [{ position: 30 } ]}
      @Consumption.valueHelpDefinition: [{ entity:{name: 'ZWAGCDS_TOOLS_VH', element: 'Toolname' } }]
      @EndUserText.label: 'Nome Ferramenta'
      _user_tools.toolname  as Toolname,
      @UI.lineItem: [{ position: 50 }]
      _user_tools.knowledge as Knowledge,
      @UI.lineItem: [{ position: 60 }]
      _user_tools.interest  as Interest,
      @UI: { lineItem: [{ position: 70, label: 'Treinamento' }],
             selectionField: [{ position: 40 } ]}
      @EndUserText.label: 'Posso dar Treinamento'
      _user_tools.training  as Training,

      _user_tools

}
